*
   GIT.FILENAME = 'IMPORT.ADVENTURE.WORKS'
   GIT.REPO = 'https://github.com/Krowemoh/AdventureWorks-for-Pick.git'
*
   EQU DICT.TYPE TO 1
   EQU DICT.FIELD TO 2
   EQU DICT.DESCRIPTION TO 3
   EQU DICT.CONVERSION TO 7
   EQU DICT.CORRELATIVE TO 8
   EQU DICT.JUSTIFICATION TO 9
   EQU DICT.WIDTH TO 10
*
   DIM ATTRIBUTE.ITEM(10)
   DIM NAME.ITEM(10)
*
   MAT ATTRIBUTE.ITEM = ''
   MAT NAME.ITEM = ''
*
   PATH = '/home/nivethan/bp/AdventureWorks-for-Pick/src'
*
   OPENPATH PATH TO SOURCE.FILE ELSE
      PRINT 'Failed to open: ' : PATH
      STOP
   END
*
   GOSUB IMPORT.PRODUCT
*
   GOSUB IMPORT.CATEGORY
*
   GOSUB IMPORT.MODEL
*
   GOSUB IMPORT.DESCRIPTION
*
   GOSUB IMPORT.CUSTOMER
*
   GOSUB IMPORT.ORDER
*
   STOP
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.PRODUCT:NULL
*
   OPEN '','INVENTORY-FILE' TO INVENTORY.FILE ELSE
      EXECUTE 'CREATE-FILE INVENTORY-FILE 53,2,6 101,4,18'
*
      OPEN '','INVENTORY-FILE' TO INVENTORY.FILE ELSE
         PRINT 'Unable to open file: INVENTORY-FILE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE INVENTORY-FILE'
*
   FILENAME = 'INVENTORY-FILE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<-1> = 'NAME'
   ATTRIBUTES<-1> = 'CODE'
   ATTRIBUTES<-1> = 'COLOR'
   ATTRIBUTES<-1> = 'STANDARD.COST'
   ATTRIBUTES<-1> = 'LIST.PRICE'
   ATTRIBUTES<-1> = 'SIZE'
   ATTRIBUTES<-1> = 'WEIGHT'
   ATTRIBUTES<-1> = 'CATEGORY'
   ATTRIBUTES<-1> = 'MODEL'
   ATTRIBUTES<-1> = 'SELL.START.DATE'
   ATTRIBUTES<-1> = 'SELL.END.DATE'
   ATTRIBUTES<-1> = 'DISCONTINUED.DATE'
   ATTRIBUTES<-1> = 'THUMBNAIL.PHOTO'
   ATTRIBUTES<-1> = 'THUMBNAIL.PHOTO.FILENAME'
   ATTRIBUTES<-1> = 'ROW.GUID'
   ATTRIBUTES<-1> = 'MODIFIED.DATE'
*
   GOSUB BUILD.DICT
*
   READ PRODUCTS.CSV FROM SOURCE.FILE,'Product.csv' ELSE
      PRINT 'Failed to open Products.'
      STOP
   END
*
   NUMBER.OF.PRODUCTS = DCOUNT(PRODUCTS.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN PRODUCTS.CSV
*
   FOR I = 1 TO NUMBER.OF.PRODUCTS
      ROW = PRODUCTS.CSV<I>
      ROW = RAISE(ROW)
*
      ROW.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      WRITE ROW ON INVENTORY.FILE,ROW.ID
   NEXT I
*
   PRINT 'NUMBER.OF.PRODUCTS: ' : NUMBER.OF.PRODUCTS
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.CATEGORY:NULL
*
   OPEN '','CATEGORY-TABLE' TO CATEGORY.FILE ELSE
      EXECUTE 'CREATE-FILE CATEGORY-TABLE 53,2,6 101,4,18'
*
      OPEN '','CATEGORY-TABLE' TO CATEGORY.FILE ELSE
         PRINT 'Unable to open file: CATEGORY-FILE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE CATEGORY-TABLE'
*
   FILENAME = 'CATEGORY-TABLE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<-1> = 'PARENT.CATEGORY'
   ATTRIBUTES<-1> = 'TEXT'
   ATTRIBUTES<-1> = 'ROW.GUID'
   ATTRIBUTES<-1> = 'MODIFIED.DATE'
*
   GOSUB BUILD.DICT
*
   READ CATEGORY.CSV FROM SOURCE.FILE,'ProductCategory.csv' ELSE
      PRINT 'Failed to open: ProductCategory.csv.'
      STOP
   END
*
   NUMBER.OF.CATEGORIES = DCOUNT(CATEGORY.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN CATEGORY.CSV
*
   FOR I = 1 TO NUMBER.OF.CATEGORIES
      ROW = CATEGORY.CSV<I>
      ROW = RAISE(ROW)
*
      ROW.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      WRITE ROW ON CATEGORY.FILE,ROW.ID
   NEXT I
*
   PRINT 'NUMBER.OF.CATEGORIES: ' : NUMBER.OF.CATEGORIES
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.MODEL:NULL
*
   OPEN '','MODEL-TABLE' TO MODEL.FILE ELSE
      EXECUTE 'CREATE-FILE MODEL-TABLE 53,2,6 101,4,18'
*
      OPEN '','MODEL-TABLE' TO MODEL.FILE ELSE
         PRINT 'Unable to open file: MODEL-TABLE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE MODEL-TABLE'
*
   FILENAME = 'MODEL-TABLE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<-1> = 'NAME'
   ATTRIBUTES<-1> = 'CATALOG.DESCRIPTION'
   ATTRIBUTES<-1> = 'ROW.GUD'
   ATTRIBUTES<-1> = 'MODIFIED.DATE'
*
   GOSUB BUILD.DICT
*
   READ MODEL.CSV FROM SOURCE.FILE,'ProductModel.csv' ELSE
      PRINT 'Failed to open: ProductModel.csv.'
      STOP
   END
*
   NUMBER.OF.MODELS = DCOUNT(MODEL.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN MODEL.CSV
*
   FOR I = 1 TO NUMBER.OF.MODELS
      ROW = MODEL.CSV<I>
      ROW = RAISE(ROW)
*
      ROW.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      WRITE ROW ON MODEL.FILE,ROW.ID
   NEXT I
*
   PRINT 'NUMBER.OF.MODELS: ' : NUMBER.OF.MODELS
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.DESCRIPTION:NULL
*
   OPEN '','PRODUCT-DESCRIPTION-TABLE' TO PRODUCT.DESCRIPTION.FILE ELSE
      EXECUTE 'CREATE-FILE PRODUCT-DESCRIPTION-TABLE 53,2,6 101,4,18'
*
      OPEN '','PRODUCT-DESCRIPTION-TABLE' TO PRODUCT.DESCRIPTION.FILE ELSE
         PRINT 'Unable to open file: PRODUCT-DESCRIPTION-TABLE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE PRODUCT-DESCRIPTION-TABLE'
*
   FILENAME = 'PRODUCT-DESCRIPTION-TABLE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<-1> = 'TEXT'
   ATTRIBUTES<-1> = 'ROW.GUD'
   ATTRIBUTES<-1> = 'MODIFIED.DATE'
*
   GOSUB BUILD.DICT
*
   READ PRODUCT.DESCRIPTION.CSV FROM SOURCE.FILE,'ProductDescription.csv' ELSE
      PRINT 'Failed to open: ProductDescription.csv.'
      STOP
   END
*
   NUMBER.OF.DESCRIPTIONS = DCOUNT(PRODUCT.DESCRIPTION.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN PRODUCT.DESCRIPTION.CSV
*
   FOR I = 1 TO NUMBER.OF.DESCRIPTIONS
      ROW = PRODUCT.DESCRIPTION.CSV<I>
      ROW = RAISE(ROW)
*
      ROW.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      WRITE ROW ON PRODUCT.DESCRIPTION.FILE,ROW.ID
   NEXT I
*
   PRINT 'NUMBER.OF.DESCRIPTIONS: ' : NUMBER.OF.DESCRIPTIONS
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.CUSTOMER:NULL
*
   OPEN '','CUSTOMER-FILE' TO CUSTOMER.FILE ELSE
      EXECUTE 'CREATE-FILE CUSTOMER-FILE 53,2,6 101,4,18'
*
      OPEN '','CUSTOMER-FILE' TO CUSTOMER.FILE ELSE
         PRINT 'Unable to open file: CUSTOMER-FILE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE CUSTOMER-FILE'
*
   FILENAME = 'CUSTOMER-FILE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<1> = 'NAME.STYLE'
   ATTRIBUTES<2> = 'TITLE'
   ATTRIBUTES<3> = 'FIRST.NAME'
   ATTRIBUTES<4> = 'MIDDLE.NAME'
   ATTRIBUTES<5> = 'LAST.NAME'
   ATTRIBUTES<6> = 'SUFFIX'
   ATTRIBUTES<7> = 'COMPANY.NAME'
   ATTRIBUTES<8> = 'SALESPERSON'
   ATTRIBUTES<9> = 'EMAIL.ADDRESS'
   ATTRIBUTES<10> = 'PHONE'
   ATTRIBUTES<11> = 'PASSWORD.HASH'
   ATTRIBUTES<12> = 'PASSWORD.SALT'
   ATTRIBUTES<13> = 'ROW.GUID'
   ATTRIBUTES<14> = 'MODIFIED.DATE'
*
   ATTRIBUTES<15> = 'ADDRESS.TYPE'
   ATTRIBUTES<16> = 'LINE1'
   ATTRIBUTES<17> = 'LINE2'
   ATTRIBUTES<18> = 'CITY'
   ATTRIBUTES<19> = 'STATE.PROVINCE'
   ATTRIBUTES<20> = 'COUNTRY.REGION'
   ATTRIBUTES<21> = 'POSTAL.CODE'
*
   GOSUB BUILD.DICT
*
   READ CUSTOMER.CSV FROM SOURCE.FILE,'Customer.csv' ELSE
      PRINT 'Failed to open: Customer.csv.'
      STOP
   END
*
   NUMBER.OF.CUSTOMERS = DCOUNT(CUSTOMER.CSV,@AM)
*
   READ ADDRESS.CSV FROM SOURCE.FILE,'Address.csv' ELSE
      PRINT 'Failed to open: Address.csv.'
      STOP
   END
   NUMBER.OF.ADDRESSES = DCOUNT(ADDRESS.CSV,@AM)
*
   READ CUSTOMER.ADDRESS.CSV FROM SOURCE.FILE,'CustomerAddress.csv' ELSE
      PRINT 'Failed to open: CustomerAddress.csv.'
      STOP
   END
*
   NUMBER.OF.RECORDS = DCOUNT(CUSTOMER.ADDRESS.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN CUSTOMER.CSV
   CONVERT CHAR(9) TO @VM IN ADDRESS.CSV
   CONVERT CHAR(9) TO @VM IN CUSTOMER.ADDRESS.CSV
*
   FOR I = 1 TO NUMBER.OF.CUSTOMERS
      ROW = CUSTOMER.CSV<I>
      ROW = RAISE(ROW)
*
      CUSTOMER.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      CTR = 0
*
      FOR J = 1 TO NUMBER.OF.RECORDS
         CUSTOMER.ADDRESS = CUSTOMER.ADDRESS.CSV<J>
*
         IF CUSTOMER.ID = CUSTOMER.ADDRESS<1,1> THEN
            ADDRESS.ID = CUSTOMER.ADDRESS<1,2>
            ADDRESS.TYPE = CUSTOMER.ADDRESS<1,3>
*
            FOR K = 1 TO NUMBER.OF.ADDRESSES
               ADDRESS = ADDRESS.CSV<K>
*
               IF ADDRESS.ID = ADDRESS<1,1> THEN
                  CTR = CTR + 1
                  ROW<15,CTR> = ADDRESS.TYPE
                  ROW<16,CTR> = ADDRESS<1,2>
                  ROW<17,CTR> = ADDRESS<1,3>
                  ROW<18,CTR> = ADDRESS<1,4>
                  ROW<19,CTR> = ADDRESS<1,5>
                  ROW<20,CTR> = ADDRESS<1,6>
                  ROW<21,CTR> = ADDRESS<1,7>
               END
            NEXT K
         END
      NEXT J
*
      WRITE ROW ON CUSTOMER.FILE,CUSTOMER.ID
   NEXT I
*
   PRINT 'NUMBER.OF.CUSTOMERS: ' : NUMBER.OF.CUSTOMERS
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
IMPORT.ORDER:NULL
*
   OPEN '','ORDER-FILE' TO ORDER.FILE ELSE
      EXECUTE 'CREATE-FILE ORDER-FILE 53,2,6 101,4,18'
*
      OPEN '','ORDER-FILE' TO ORDER.FILE ELSE
         PRINT 'Unable to open file: ORDER-FILE - Press RETURN':
         INPUT ANYTHING
         STOP
      END
   END
*
   EXECUTE 'CLEAR-FILE ORDER-FILE'
*
   FILENAME = 'ORDER-FILE'
*
   ATTRIBUTES = ''
   ATTRIBUTES<1> = 'REVISION.#'
   ATTRIBUTES<2> = 'ORDER.DATE'
   ATTRIBUTES<3> = 'DUE.DATE'
   ATTRIBUTES<4> = 'SHIP.DATE'
   ATTRIBUTES<5> = 'STATUS'
   ATTRIBUTES<6> = 'ONLINE.ORDER.FLAG'
   ATTRIBUTES<7> = 'SALES.ORDER.#'
   ATTRIBUTES<8> = 'PURCHASE.ORDER.#'
   ATTRIBUTES<9> = 'ACCOUNT.#'
   ATTRIBUTES<10> = 'CUSTOMER.#'
   ATTRIBUTES<11> = 'SHIP.TO.ADDRESS'
   ATTRIBUTES<12> = 'BILL.TO.ADDRESS'
   ATTRIBUTES<13> = 'SHIP.METHOD'
   ATTRIBUTES<14> = 'CREDIT.CARD.APPROVAL.CODE'
   ATTRIBUTES<15> = 'SUB.TOTAL'
   ATTRIBUTES<16> = 'TAX.AMOUNT'
   ATTRIBUTES<17> = 'FREIGHT'
   ATTRIBUTES<18> = 'TOTAL.DUE'
   ATTRIBUTES<19> = 'COMMENT'
   ATTRIBUTES<20> = 'ROW.GUID'
   ATTRIBUTES<21> = 'MODIFIED.DATE'
*
   ATTRIBUTES<22> = 'PRODUCT'
   ATTRIBUTES<23> = 'QTY.ORDERED'
   ATTRIBUTES<24> = 'UNIT.PRICE'
   ATTRIBUTES<25> = 'UNIT.PRICE.DISCOUNT'
   ATTRIBUTES<26> = 'LINE.TOTAL'
*
   GOSUB BUILD.DICT
*
   READ ORDER.CSV FROM SOURCE.FILE,'SalesOrderHeader.csv' ELSE
      PRINT 'Failed to open: SalesOrderHeader.csv.'
      STOP
   END
   NUMBER.OF.ORDERS = DCOUNT(ORDER.CSV,@AM)
*
   READ ADDRESS.CSV FROM SOURCE.FILE,'Address.csv' ELSE
      PRINT 'Failed to open: Address.csv.'
      STOP
   END
   NUMBER.OF.ADDRESSES = DCOUNT(ADDRESS.CSV,@AM)
*
   READ ORDER.LINE.CSV FROM SOURCE.FILE,'SalesOrderDetail.csv' ELSE
      PRINT 'Failed to open: SalesOrderDetail.csv.'
      STOP
   END
   NUMBER.OF.LINES = DCOUNT(ORDER.LINE.CSV,@AM)
*
   CONVERT CHAR(9) TO @VM IN ORDER.CSV
   CONVERT CHAR(9) TO @VM IN ADDRESS.CSV
   CONVERT CHAR(9) TO @VM IN ORDER.LINE.CSV
*
   FOR I = 1 TO NUMBER.OF.ORDERS
      ROW = ORDER.CSV<I>
      ROW = RAISE(ROW)
*
      ORDER.ID = ROW<1>
      ROW = DELETE(ROW,1)
*
      SHIP.TO.ID = ROW<11>
      FOR J = 1 TO NUMBER.OF.ADDRESSES
         ADDRESS = ADDRESS.CSV<J>
         IF SHIP.TO.ID = ADDRESS<1,1> THEN
            ADDRESS = DELETE(ADDRESS,1,1)
            ADDRESS = DELETE(ADDRESS,1,DCOUNT(ADDRESS,@VM))
            ADDRESS = DELETE(ADDRESS,1,DCOUNT(ADDRESS,@VM))
            ROW<11> = ADDRESS
            EXIT
         END
      NEXT J
*
      BILL.TO.ID = ROW<12>
      FOR J = 1 TO NUMBER.OF.ADDRESSES
         ADDRESS = ADDRESS.CSV<J>
         IF BILL.TO.ID = ADDRESS<1,1> THEN
            ADDRESS = DELETE(ADDRESS,1,1)
            ADDRESS = DELETE(ADDRESS,1,DCOUNT(ADDRESS,@VM))
            ADDRESS = DELETE(ADDRESS,1,DCOUNT(ADDRESS,@VM))
            ROW<12> = ADDRESS
            EXIT
         END
      NEXT J
*
      CTR = 0
      FOR J = 1 TO NUMBER.OF.LINES
         ORDER.LINE = ORDER.LINE.CSV<J>
*
         IF ORDER.ID = ORDER.LINE<1,1> THEN
            CTR = CTR + 1
            ROW<22,CTR> = ORDER.LINE<1,4> 
            ROW<23,CTR> = ORDER.LINE<1,3> 
            ROW<24,CTR> = ORDER.LINE<1,5> 
            ROW<25,CTR> = ORDER.LINE<1,6> 
            ROW<26,CTR> = ORDER.LINE<1,7> 
         END
      NEXT J
*
      WRITE ROW ON ORDER.FILE,ORDER.ID
   NEXT I
*
   PRINT 'NUMBER.OF.ORDERS: ' : NUMBER.OF.ORDERS
   PRINT
*
   RETURN
*
*********************  S U B R O U T I N E  *********************
*
BUILD.DICT:NULL
*
   OPEN 'DICT',FILENAME TO DICT.FILE ELSE
      PRINT 'Unable to open file: DICT ' : FILENAME : ' - Press RETURN':
      INPUT ANYTHING
      STOP
   END
*
   AT.RECORD = 'PH'
   NUMBER.OF.ATTRIBUTES = DCOUNT(ATTRIBUTES,@AM)
*
   FOR ATTR.CTR = 1 TO NUMBER.OF.ATTRIBUTES
      AT.RECORD<2> = AT.RECORD<2> : ' ' : ATTR.CTR
*
      ATTRIBUTE.NUMBER = ATTR.CTR
      ATTRIBUTE.NAME = ATTRIBUTES<ATTR.CTR>
*
      NAME.ITEM(DICT.TYPE) = 'A'
      NAME.ITEM(DICT.FIELD) = ATTRIBUTE.NUMBER
      NAME.ITEM(DICT.DESCRIPTION) = ATTRIBUTE.NAME
      NAME.ITEM(4) = ''
      NAME.ITEM(5) = ''
      NAME.ITEM(6) = ''
      NAME.ITEM(DICT.CONVERSION) = ''
      NAME.ITEM(DICT.CORRELATIVE) = ''
      NAME.ITEM(DICT.JUSTIFICATION) = 'L'
      NAME.ITEM(DICT.WIDTH) = 25
*
      MATWRITE NAME.ITEM ON DICT.FILE,ATTRIBUTE.NAME
*
      ATTRIBUTE.ITEM(DICT.TYPE) = 'S'
      ATTRIBUTE.ITEM(DICT.FIELD) = ATTRIBUTE.NUMBER
      ATTRIBUTE.ITEM(DICT.DESCRIPTION) = ATTRIBUTE.NAME
      ATTRIBUTE.ITEM(4) = ''
      ATTRIBUTE.ITEM(5) = ''
      ATTRIBUTE.ITEM(6) = ''
      ATTRIBUTE.ITEM(DICT.CONVERSION) = ''
      ATTRIBUTE.ITEM(DICT.CORRELATIVE) = ''
      ATTRIBUTE.ITEM(DICT.JUSTIFICATION) = 'L'
      ATTRIBUTE.ITEM(DICT.WIDTH) = 25
*
      MATWRITE ATTRIBUTE.ITEM ON DICT.FILE,ATTRIBUTE.NUMBER
   NEXT ATTR
*
   WRITE AT.RECORD ON DICT.FILE,'@'
*
   RETURN
*
* END OF PROGRAM
*
   END
*
